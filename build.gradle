buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
    }
}
// Apply the java plugin to add support for Java
subprojects {
    apply plugin: 'java'

    // In this section you declare where to find the dependencies of your project
    repositories {
        mavenCentral();
    }

    // In this section you declare the dependencies for your production and test code
    dependencies {
        // The production code uses the SLF4J logging API at compile time
        compile 'org.slf4j:slf4j-api:1.7.7'

        // Declare the dependency for your favourite test framework you want to use in your tests.
        // TestNG is also supported by the Gradle Test task. Just change the
        // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
        // 'test.useTestNG()' to your build script.
        testCompile "junit:junit:4.11"
    }
}

project(':jna') {
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    dependencies {
        compile group: 'net.java.dev.jna', name: 'jna', version: '4.2.2'
    }
    mainClassName = 'HelloWorldJNA'
}

project(':protobuf') {
    apply plugin: 'com.google.protobuf'
    dependencies {
        // https://mvnrepository.com/artifact/io.grpc/grpc-all
        compile group: 'io.grpc', name: 'grpc-all', version: '1.0.0-pre2'
    }
    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.0.0'
        }
        plugins {
            grpc {
                // Be sure the gRPC versions match
                artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {
                    // Defaults to 'grpc', could just add that to sourceSets instead
                    outputSubDir = 'java'
                }
            }
        }
        // Defaults to somewhere in the build directory
        generatedFilesBaseDir = "$projectDir/src"
    }
}

project(':rpc') {
    dependencies {
        compile project(":protobuf")
        compile 'io.grpc:grpc-all:0.15.0'
        compile 'org.threadly:threadly:4.6.2'
    }
}
